@Data_API

import "platform:/resource/SokobanLanguage/src/metamodels/SokobanLanguage.msl"

pattern board {
	board:SokobanLanguage.Board
}

pattern fields {
	board:SokobanLanguage.Board {
		-fields->field
	}
	field:Field
}

pattern occupied {
	field:Field
	fig:Figure {
		-field->field	
	}
}

pattern emptyFields -> fields when forbid occupied

pattern occupiedFields -> fields, occupied {
	fig:Figure
} 

pattern figureTypes {
	figClass:EClass {
		.ename : "Figure"
		.enamespace : mm::ename
	}
	
	eclass:EClass {
		.enamespace : mm::ename
		-eSuperType(1..*)->figClass
	}
	
	mm:MetaModel {
		.ename : "SokobanLanguage"
	}
}

pattern selectedFigure {
	b:SokobanLanguage.Board {
		-selectedFigure->fig
		-fields->f
	}
	
	fig:Figure {
		-field->f
	}
	
	f:Field
}

pattern oneSokoban { sok:Sokoban }
pattern twoSokobans -> oneSokoban, oneSokoban[sok->otherSok]

constraint oneSokoban = enforce oneSokoban
constraint noTwoSokobans = forbid twoSokobans
constraint exactlyOneSokoban = oneSokoban && noTwoSokobans

pattern endField { 
	endField:Field { 
		.endPos : true
	}
}

constraint oneEndField = enforce endField

pattern block { boulder:Block }

pattern endFieldBlockedByBoulder -> endField {
	boulder : Boulder {
		-field-> $endField
	}
} when forbid block

rule deleteFigure -> chooseField {
	-- fig:Figure {
		-- -field->$f
	}
}

abstract rule createFigure -> chooseField {
	++ fig:Figure {
		++ -field-> $f
	}
}

pattern otherFigureOnField {
	other:Figure {
		-field->f
	}
	
	f:Field
}

rule createSokoban -> createFigure {
	++ fig:Sokoban {
		.ename := "Sokoban"
	}
} when forbid otherFigureOnField

rule createBlock -> createFigure {
	++ fig:Block {
		.ename := "Block"
	}
} when forbid otherFigureOnField

rule createBoulder -> createFigure {
	++ fig:Boulder {
		.ename := "Boulder"
	}
} when forbid otherFigureOnField

abstract rule chooseField {
	b:SokobanLanguage.Board {
		-fields->f
	}
	
	f:Field
}

rule setEndField -> chooseField {
	f:Field {
		.endPos := true	
	}
}

rule setNotEndField -> chooseField {
	f:Field {
		.endPos := false
	}
}

rule selectFigure {
	f:Field
	
	newFig:Figure {
		-field->f
	}
	
	fig:Figure
	
	b:SokobanLanguage.Board {
		-fields->f
		-- -selectedFigure->fig
		++ -selectedFigure->newFig
	}
}

// Rules for generating boards

abstract rule createField {
	++ f:Field {
		.endPos := false
	}
}

rule createTopLeft -> createField {
	++ b:SokobanLanguage.Board {
		++ -fields->$f {
			.col := 0
			.row := 0
		}
	}
}

rule createTopLeftWithAppCond -> createTopLeft when forbid TopLeft

pattern TopLeft {
	otherBoard:SokobanLanguage.Board {
		-fields->otherField {
			.col : 0
			.row : 0
		}
	}
	
	otherField:Field
}

rule createFirstRow -> createField[f -> rightField] {
	b:SokobanLanguage.Board {
		-fields->leftField {
			.row : 0
		}
		
		++ -fields->rightField {
			.row := 0
			.col := b::-fields->leftField::col + 1
		}
	}
	
	leftField:Field {
		++ -right->rightField
	}
	
	++ rightField:Field
} when forbid OtherRight

rule createFirstCol -> createField[f -> bottomField] {
	b:SokobanLanguage.Board {
		-fields->topField {
			.col : 0
		}
		
		++ -fields->bottomField {
			.col := 0
			.row := b::-fields->topField::row + 1
		}
	}
	
	topField:Field {
		++ -bottom->bottomField
	}
	
	++ bottomField:Field
} when forbid OtherBottom

rule createRestOfFields -> createField[f -> field] {
	b:SokobanLanguage.Board {
		-fields->leftField
		-fields->topField
		-fields->cornerField
		++ -fields->field {
			.col := b::-fields->topField::col
			.row := b::-fields->leftField::row
		}
	}
	
	cornerField:Field {
		-right->topField
		-bottom->leftField
	}
	
	leftField:Field {
		++ -right->field
	}
	
	topField:Field {
		++ -bottom->field
	}
	
	++ field:Field
} when noBottomAndNoRight

constraint NoOtherBottom = forbid OtherBottom
constraint NoOtherRight = forbid OtherRight
constraint noBottomAndNoRight = NoOtherBottom && NoOtherRight

pattern OtherBottom {
	topField:Field {
		-bottom->otherField
	}
	
	otherField:Field
}

pattern OtherRight {
	leftField:Field {
		-right->otherField
	}
	
	otherField:Field
}