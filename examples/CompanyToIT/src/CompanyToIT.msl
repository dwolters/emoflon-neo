import "platform:/resource/CompanyToIT/src/metamodels/Company.msl"
import "platform:/resource/CompanyToIT/src/metamodels/IT.msl"

tripleGrammar CompanyToIT {
	source {
		Company
	}
	
	target {
		IT
	}
	
	correspondence {
		Company.Company <- CompanyToIT -> IT.IT
		Company.Admin <- AdminToRouter -> IT.Router
		Company.Employee <- EmployeeToPC -> IT.PC
		Company.Employee <- EmployeeToLaptop -> IT.Laptop
	}
	
	rules {
		CompanyToITRule
		AdminToRouterRule
		EmployeeToPCRule
		EmployeeToLaptopRule
	}
}

tripleRule CompanyToITRule : CompanyToIT {
	source {
		++ ceo : CEO
		++ company : Company {
			++ -Company.ceo-> ceo
		}
	}
	
	target {
		++ it : IT
	}
	
	correspondence {
		++ company <-:CompanyToIT-> it
	}
}


tripleRule AdminToRouterRule : CompanyToIT {
	source {
		company : Company {
			-Company.ceo-> ceo
			++ -admin-> admin
		}
		ceo : CEO
		++ admin : Admin {
			++ -Admin.ceo-> ceo
		}
	} 

	target {
		++ router : Router {
			++ -Router.network-> network
		}
		it : IT {
			++ -IT.network-> network
			++ -router-> router
		}
		++ network : Network
	}

	correspondence {
		++ admin <-:AdminToRouter-> router
		company <-:CompanyToIT-> it
	}
}

tripleRule EmployeeToPCRule : CompanyToIT {
	source {
		admin : Admin {
			-Admin.ceo-> ceo
		}
		company : Company {
			-admin-> admin
			++ -Company.employee-> employee
			-Company.ceo-> ceo
		}
		ceo : CEO {
			++ -CEO.employee-> employee
		}
		++ employee : Employee
	}

	target {
		network : Network {
			++ -pc-> computer
		}
		it : IT {
			-router-> router
			-IT.network-> network
		}
		++ computer : PC
		router : Router {
			-Router.network-> network
		}
	}

	correspondence {
		++ employee <-:EmployeeToPC-> computer
		admin <-:AdminToRouter-> router
		company <-:CompanyToIT-> it
	}
}

tripleRule EmployeeToLaptopRule : CompanyToIT {
	source {
		admin : Admin {
			-Admin.ceo-> ceo
		}
		company : Company {
			-admin-> admin
			++ -Company.employee-> employee
			-Company.ceo-> ceo
		}
		ceo : CEO {
			++ -CEO.employee-> employee
		}
		++ employee : Employee
	}

	target {
		network : Network {
			++ -laptop-> computer
		}
		it : IT {
			-router-> router
			-IT.network-> network
		}
		++ computer : Laptop
		router : Router {
			-Router.network-> network
		}
	}

	correspondence {
		++ employee <-:EmployeeToLaptop-> computer
		admin <-:AdminToRouter-> router
		company <-:CompanyToIT-> it
	}
}